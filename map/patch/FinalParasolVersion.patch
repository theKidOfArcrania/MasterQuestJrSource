#new:Data $WarpsValueTable
{
$ToadTownValues			$KoopaBrosFortressValues
$DryDryRuinsValues		$BoosMansionValues
$TubbasCastleValues 	$ToyBoxValues
$YoshisVillageValues	$FlowerFieldsValues
$CrystalPalaceValues	$BowsersCastleValues
$PeachsCastleValues		$PitOf100Values
$StarTempleValues		00000000 %terminator for function
}

#new:Data $TravelTextPointers
{
$ToadTownText			$KoopaBrosFortressText
$DryDryRuinsText		$BoosMansionText
$TubbasCastleText		$ToyBoxText
$YoshisVillageText		$FlowerFieldsText
$CrystalPalaceText		$BowsersCastleText
$PeachsCastleText		$PitOf100Text
$StarTempleText			00000000 %terminator for function
	%second half are disabled text pointers - if encountering a disabled text pointer, system skips ahead 14 words to get the disable pointer
$ToadTownTextDis		$KoopaBrosFortressTextDis
$DryDryRuinsTextDis		$BoosMansionTextDis
$TubbasCastleTextDis	$ToyBoxTextDis
$YoshisVillageTextDis	$FlowerFieldsTextDis
$CrystalPalaceTextDis	$BowsersCastleTextDis
$PeachsCastleTextDis	$PitOf100TextDis
$StarTempleTextDis	00000000 %terminator for function
}

#new:Data $DynamicallyGeneratedWarpValues
{
%slightly different from the above, this is generated
%based on what maps you have visited, so you cant warp to places you dont have on the map screen
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000 %terminator for function
}

#new:Data $DynamicallyGeneratedTextPointers
{
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000
00000000 00000000 %terminator for function
}


#new:Data $NoWarpText
{
FF0D0B0B FF0500F7 394F55F7 43414E07
54F75741 5250F74E 4F570EFD
}

#new:Data $ToadTownText
{
FF0D0B0B FF0500F7 344F4144 F7344F57
4EFDFDFD
}

#new:Data $ToadTownTextDis
{
FF0D0B0B FF0517F7 344F4144 F7344F57 %color byte - third byte of second word - set to 17 (gray)
4EFDFDFD
}

#new:Data $KoopaBrosFortressText
{
FF0D0B0B FF0500F7 2B4F4F50 41F72252
4F53F726 4F525452 455353FD
}

#new:Data $KoopaBrosFortressTextDis
{
FF0D0B0B FF0517F7 2B4F4F50 41F72252
4F53F726 4F525452 455353FD
}

#new:Data $DryDryRuinsText
{
FF0D0B0B FF0500F7 245259F7 245259F7
3255494E 53FDFDFD
}

#new:Data $DryDryRuinsTextDis
{
FF0D0B0B FF0517F7 245259F7 245259F7
3255494E 53FDFDFD
}

#new:Data $BoosMansionText
{
FF0D0B0B FF0500F7 224F4F07 53F72D41
4E53494F 4EFDFDFD
}

#new:Data $BoosMansionTextDis
{
FF0D0B0B FF0517F7 224F4F07 53F72D41
4E53494F 4EFDFDFD
}

#new:Data $TubbasCastleText
{
FF0D0B0B FF0500F7 34554242 410753F7
23415354 4C45FDFD
}

#new:Data $TubbasCastleTextDis
{
FF0D0B0B FF0517F7 34554242 410753F7
23415354 4C45FDFD
}

#new:Data $ToyBoxText
{
FF0D0B0B FF0500F7 344F59F7 224F58FD
}

#new:Data $ToyBoxTextDis
{
FF0D0B0B FF0517F7 344F59F7 224F58FD
}

/%
#new:Data $MtLavaLavaText
{
FF0D0B0B FF0500F7 2D540EF7 2C415641  %F7 = space %letters are +20
2C415641 FDFDFDFD
}%/

#new:Data $YoshisVillageText
{
FF0D0B0B FF0500F7 394F5348 490753F7  %F7 = space %letters are +20
36494C4C 414745FD
}

#new:Data $YoshisVillageTextDis
{
FF0D0B0B FF0517F7 394F5348 490753F7  %F7 = space %letters are +20
36494C4C 414745FD
}

#new:Data $FlowerFieldsText
{
FF0D0B0B FF0500F7 264C4F57 4552F726
49454C44 53FDFDFD
}

#new:Data $FlowerFieldsTextDis
{
FF0D0B0B FF0517F7 264C4F57 4552F726
49454C44 53FDFDFD
}

#new:Data $CrystalPalaceText
{
FF0D0B0B FF0500F7 23525953 54414CF7
30414C41 4345FDFD
}

#new:Data $CrystalPalaceTextDis
{
FF0D0B0B FF0517F7 23525953 54414CF7
30414C41 4345FDFD
}

#new:Data $BowsersCastleText
{
FF0D0B0B FF0500F7 224F5753 45520753
F7234153 544C45FD
}

#new:Data $BowsersCastleTextDis
{
FF0D0B0B FF0517F7 224F5753 45520753
F7234153 544C45FD
}

#new:Data $PeachsCastleText
{
FF0D0B0B FF0500F7 30454143 480753F7
23415354 4C45FDFD
}

#new:Data $PeachsCastleTextDis
{
FF0D0B0B FF0517F7 30454143 480753F7
23415354 4C45FDFD
}

#new:Data $PitOf100Text
{
FF0D0B0B FF0500F7 304954F7 4F46F711 
1010F734 5249414C 53FDFDFD
}

#new:Data $PitOf100TextDis
{
FF0D0B0B FF0517F7 304954F7 4F46F711 
1010F734 5249414C 53FDFDFD
}

#new:Data $StarTempleText
{
FF0D0B0B FF0500F7 33544152 F734454D
504C45FD
}

#new:Data $StarTempleTextDis
{
FF0D0B0B FF0517F7 30454143 480753F7
23415354 4C45FDFD
}

%last byte of each 4 byte is unused

#new:Data $ToadTownValues
{
01020000
}

#new:Data $KoopaBrosFortressValues
{
07000000
}

#new:Data $DryDryRuinsValues
{
0B000000
}

#new:Data $BoosMansionValues
{
0C0A0000
}

#new:Data $TubbasCastleValues
{
0F000000
}

#new:Data $ToyBoxValues
{
10020400
}

#new:Data $YoshisVillageValues
{
11020000
}

/%
#new:Data $MtLavaLavaValues
{
12000000
}
%/

#new:Data $FlowerFieldsValues
{
13000000
}

#new:Data $CrystalPalaceValues
{
15000000
}

#new:Data $BowsersCastleValues
{
16170300
}

#new:Data $PeachsCastleValues
{
17020000
}

#new:Data $PitOf100Values
{
00110100
}

#new:Data $StarTempleValues
{
050A0100
}

#new:Data $ValueForTesting
{
00000000
}

#new:Data $CursorAnimation
{
00000000
}

#new:Data $YPosCursor
{
0000003A
}

#new:Data $AFlag
{
00000000
}

#new:Function $DrawMenuBox
{
ADDIU sp, sp, -68
%%%%%%%%%%%%%%%%%%%%
SW  R0, 0010 (SP)
SW  R0, 0020 (SP)
SW  R0, 0024 (SP)
SW  R0, 0028 (SP)
SW  R0, 002C (SP)
SW  R0, 0030 (SP)
SW  R0, 0034 (SP)
SW  R0, 0038 (SP)
SW  R0, 003C (SP)
SW  R0, 0040 (SP)
SW	R0, 004C (SP)
%%%%%%%%%%%%%%%%%%%%
SW a0, 0050 (sp)
SW a1, 0054 (sp)
SW a2, 0058 (sp)
SW a3, 005C (sp)
SW ra, 0060 (sp)
LIO a0, 00FF %opacity
SW a0, 001C (sp)
ORI a0, r0, 0080 %width of box
SW a0, 0014 (sp) %store width
%ORI a0, r0, 007C %length of box
LAW a0, $AmountOfPointersToLoad
ORI a1, r0, 000B
MULT a0, a1
MFLO a1
ADDIU A0, A0, FFFE %add extra space if only a few items
BLEZL  A0, .addLowerSpace
	ADDIU A1, A1, 0005
.addLowerSpace
ADDIU a1, a1, 0005
SW a1, 0018 (sp) %store length
ORI a0, r0, 0000
ORI a1, r0, 0004 %style
ORI a2, r0, 000C %x pos
ORI a3, r0, 0035 %y pos
JAL ~Func:draw_box
NOP
LW a0, 0050 (sp)
LW a1, 0054 (sp)
LW a2, 0058 (sp)
LW a3, 005C (sp)
LW ra, 0060 (sp)
JR RA
ADDIU sp, sp, 68
}


#new:Function $DrawingMenuBoxCursor
{
ADDIU sp, sp, -68
SW a0, 0050 (sp)	   %moved from below to clear A0
LAB		A0, 800DBD7B   %load WarpLock
BLTZ	A0, .end	   %locked: skip this
%%%%%%%%%%%%%%%%%%%%
SW  R0, 0010 (SP)
SW  R0, 0020 (SP)
SW  R0, 0024 (SP)
SW  R0, 0028 (SP)
SW  R0, 002C (SP)
SW  R0, 0030 (SP)
SW  R0, 0034 (SP)
SW  R0, 0038 (SP)
SW  R0, 003C (SP)
SW  R0, 0040 (SP)
SW	R0, 004C (SP)
%%%%%%%%%%%%%%%%%%%%
SW a1, 0054 (sp)
SW a2, 0058 (sp)
SW a3, 005C (sp)
SW ra, 0060 (sp)
LIO a0, 00FF %opacity
SW a0, 001C (sp)
ORI a0, r0, 0070 %width of box
SW a0, 0014 (sp) %store width
ORI a0, r0, 000C %length of box
SW a0, 0018 (sp) %store length
ORI a0, r0, 0000
ORI a1, r0, 0010 %style
ORI a2, r0, 000C %x pos
%ORI a3, r0, 003A %y pos
LAW a3, $YPosCursor
JAL ~Func:draw_box
NOP
LW a1, 0054 (sp)
LW a2, 0058 (sp)
LW a3, 005C (sp)
LW ra, 0060 (sp)
.end
LW a0, 0050 (sp) %we now clear A0 last so we can jump to it if we bailed out
JR RA
ADDIU sp, sp, 68
}


#new:Function $FlagFlipper
{
PUSH t0, t1, ra
ORI t0, r0, 0060
LAB t1, 8010EFCA
BEQ t0, t1, .end
NOP
LAB t0, 800DBD7A %Gamebyte[00A]
BEQ t0, r0, .end
NOP
LAB t0, 800740A2
ANDI t0, t0, 0001 %if 01, player is peach
BNE t0, r0, .end
NOP
LAB t0, 800DBD70 %if in epilogue
ORI t1, r0, 0060
BEQ t0, t1, .end
NOP
LAW t0, 8009A600
ORI t1, r0, 0005
BEQ t0, t1, .end
NOP
LAH t0, 80074036
ORI t1, r0, 0010 %r button
BNE t0, t1, .end
NOP
LAW t0, 800DBD64
ANDI t0, t0, 0400 %check if toad town has been visited (parasol wont work until toad town has been visited)
BEQ t0, r0, .end
NOP
.flagcheck
LAW t0, $AFlag
BEQ t0, r0, .flipto1
NOP
JAL $UnlockPlayerMovement
NOP
JAL $RemoveMarioInvisibilityToEnemies
NOP
SAW r0, $AFlag
.end
POP t0, t1, ra
JR ra
NOP
.flipto1
LAB t0, 8010EFCA
BNE t0, r0, .end
NOP
ORI t0, r0, 0001
SAW t0, $AFlag
JAL $LockPlayerMovement
NOP
JAL $GiveMarioInvisibiltyToEnemies
NOP
ORI t0, r0, 003A
SAW t0, $YPosCursor
BEQ r0, r0, .end
NOP
}


#new:Function $LockPlayerMovement
{
PUSH t0
ORI t0, r0, 0020
SAB t0, 8010EFCA
POP t0
JR RA
NOP
}


#new:Function $UnlockPlayerMovement
{
SAB r0, 8010EFCA
JR RA
NOP
}


#new:Function $GiveMarioInvisibiltyToEnemies
{
PUSH t0, ra
ORI t0, r0, 0009
SAW t0, 8010EBB0
POP t0, ra
JR RA
ADDIU V0, R0, 2 %for compatibility when used in scripts
}


#new:Function $RemoveMarioInvisibilityToEnemies
{
SAW r0, 8010EBB0
JR RA
ADDIU V0, R0, 2 %for compatibility when used in scripts
}

#new:Function $StoreNumberofPointersFromDynamicWarpMenu
{
PUSH t0, t1, v0, ra
LAB		V0, 800DBD7B   %load WarpLock
BLTZL	V0, .end   %if it's -1, we say 1 pointer
	ADDIU	V0, R0, 1
ORI v0, r0, 0000	   %otherwise count them
LIO t0, $DynamicallyGeneratedTextPointers %was WarpValues but changed to work with special warp lock
.loop
LW t1, 0000 (t0)
BEQ t1, r0, .end
NOP
ADDIU t0, t0, 0004
BEQ r0, r0, .loop
ADDIU v0, v0, 0001
.end
SAW v0, $AmountOfPointersToLoad
POP t0, t1, v0, ra
JR RA
NOP
}

#new:Data $AmountOfPointersToLoad
{
00000000
}

#new:Function $MainAnimation
{
PUSH t0, ra
JAL $FlagFlipper
NOP
LAW t0, $AFlag
BEQ t0, r0, .end
NOP
%Function to execute if flag is 1
JAL $ClearDynamicMenus
NOP
JAL $GenerateDynamicWarpMenu
NOP
JAL $StoreNumberofPointersFromDynamicWarpMenu
NOP
JAL $DrawMenuBox
NOP
JAL $DrawingMenuBoxCursor
NOP
JAL $PrintTravelMenu
NOP
JAL $MoveCursor
NOP
.end
POP t0, ra
JR RA
NOP
}


#new:Function $CalcCursorIntPos
{
PUSH t0, t1
LAW t0, $YPosCursor
ADDIU t1, t0, FFC6 % -3A because menu starts at this pos
ORI t2, r0, 0000
BEQ t1, r0, .end
NOP
ORI t1, r0, 003A
.loop
ADDIU t2, t2, 0001
ADDIU t1, t1, 000A
BEQ t1, t0, .end
NOP
BEQ r0, r0, .loop
NOP
.end
POP t0, t1
JR RA
NOP
}


#new:Function $MoveCursor
{
PUSH t0, t1, t2, s0, s1, ra
LAW t0, $CursorAnimation
BNE t0, r0, .inanimation
NOP
LAH t0, 80074036
ORI t1, r0, 0800 %dpad up
BEQ t0, t1, .dpadup
NOP
ORI t1, r0, 0400 %dpad down
BEQ t0, t1, .dpaddown
NOP
BEQ r0, r0, .end
NOP
.dpadup
LAW t0, $YPosCursor
ORI t1, r0, 003A %min value (y pos of highest text)
BEQ t0, t1, .end
NOP
ADDIU t0, t0, FFFB
SAW t0, $YPosCursor
ORI t0, r0, 0001
SAW t0, $CursorAnimation
BEQ r0, r0, .end
NOP
.dpaddown
ORI t0, r0, 000A
LAW t1, $AmountOfPointersToLoad
MULT t0, t1
MFLO t1
ADDIU t1, t1, 0030 %min value (y pos of lowest text) calculated here
LAW t0, $YPosCursor
%ORI t1, r0, 009E %min value (y pos of lowest text)
BEQ t0, t1, .end
NOP
ADDIU t0, t0, 0005
SAW t0, $YPosCursor
ORI t0, r0, 0002
SAW t0, $CursorAnimation
BEQ r0, r0, .end
NOP
.end
POP t0, t1, t2, s0, s1, ra
JR RA
NOP
.inanimation
LAW t2, $YPosCursor
ORI t1, r0, 0001
BEQ t0, t1, .is1 %moving down
NOP
ORI t1, r0, 0002
BEQ t0, t1, .is2
NOP
.is1
ADDIU t2, t2, FFFB
SAW t2, $YPosCursor
SAW r0, $CursorAnimation
BEQ r0, r0, .end
NOP
.is2
ADDIU t2, t2, 0005
SAW t2, $YPosCursor
SAW r0, $CursorAnimation
BEQ r0, r0, .end
NOP
}


#new:Function $PrintTravelMenu
{
PUSH a0, a1, a2, a3, v0, t0, t1, ra
LIO v0, $DynamicallyGeneratedTextPointers
ORI a2, r0, 0030
ORI t0, r0, 0000
ORI t1, r0, 0000
.loop
LW a0, 0000 (v0)
BEQ a0, r0, .end
NOP
ORI a1, r0, 000A
ADDIU a2, a2, 000A
SAW r0, 800AE51C
ADDIU sp, sp, -30
SW a0, 0018 (sp)
SW a1, 001C (sp)
SW a2, 0020 (sp)
SW a3, 0024 (sp)
SW v0, 0028 (sp)
SW t0, 0010 (sp)
SW t1, 0014 (sp)
JAL 801264EC		%args = string pointer (A0), X (A1?), Y (A2?), palette (in string), opacity (A3), style (??) 
ORI a3, r0, 00FE
LW a0, 0018 (sp)
LW a1, 001C (sp)
LW a2, 0020 (sp)
LW a3, 0024 (sp)
LW v0, 0028 (sp)
LW t0, 0010 (sp)
LW t1, 0014 (sp)
ADDIU sp, sp, 30
ADDIU v0, v0, 0004
BEQ r0, r0, .loop
NOP
.end
POP a0, a1, a2, a3, v0, t0, t1, ra
JR RA
NOP
}

%adapted from Clover's debug menu warp function
#new:Function $WarpFunction
{
	PUSH RA, S7			%0, 4, 8
	LIA            S7, 8007419C %C
	JAL $ReturnIndexFromValueTable %returns table row on A0, A1, and A2 %10
	RESERVED %14
	LW            S7, 0 (S7) %18
	SH            A0, 86 (S7)    % store area ID - 1C
    SH            A1, 8C (S7) % store map ID - 20
    SH            A2, 8E (S7) % store entry ID - 24
    JAL            801382A0 %28
    ADDIU        A0, R0, 0    % special effect type here %2C
    JAL            800334F0 %30
    ADDIU        A0, R0, 5    %force state change - should warp %34
    POP RA, S7 %your jpop song of the day is PONPONPON by Kyary %38
    JR RA
    LI  V0, 2 %return code
}
%clover's fn
 /%   PUSH        RA, S6, S7
    DADDU        S6, A1, R0
    ADDIU        SP, SP, -10
    ADDIU        A1, SP, 10
    JAL            8005ABF8 % get_map_IDs_by_name
    ADDIU        A2, SP, 12
    LHU            V0, 10 (SP)
    LHU            V1, 12 (SP)
    ADDIU        SP, SP, 10
    LIA            S7, 8007419C
    LW            A0, 0 (S7)
    SH            V0, 86 (A0)    % store area ID
    SH            V1, 8C (A0) % store map ID
    LW            V1, 0 (S7)
    SH            S6, 8E (V1) % store entry ID
    JAL            801382A0
    ADDIU        A0, R0, 0    % special effect type here
    JAL            800334F0
    ADDIU        A0, R0, 5
    JPOP        RA, S6, S7
%}%/


/%#new:Function $WarpFunction
{
%OLD VERSION - preserved for posterity
PUSH v1, t0, a0, a1, a2, ra 
%if you don't push V1 before calling ClearAreaFlags and ClearMapFlags, those JALs clobber those registers and it crashes the post-warp code... or we forgot to set V0 to 2
LIO t0, 8003617C %value to change room pointer to
SAH r0, 800A0944 %Room change state to 0
SAW t0, 80151708 %Room change pointer
JAL $ReturnIndexFromValueTable
NOP
SAH a0, 800740AA %Group address
SAH a0, 800740AC %Group address (2) %this needs to be here or AreaFlags are cleared the first time you move between rooms at your destination - this notably causes the Toy Train to misdirect you
SAH a1, 800740B0 %Room address
SAH a2, 800740B2 %Entrance address
JAL ~Func:clear_area_flags %we have to clear these ourselves since the game won't
NOP
JAL	802C31DC %ClearMapFlags - Also clears a lot of other stuff. A bit of a _hapax legomenon_, really.
NOP
ADDIU V0, R0, 2 %we forgot to add this the first time
POP v1, t0, a0, a1, a2, ra
JR RA
NOP
}%/

#new:Function $ReturnIndexFromValueTable
{
PUSH t0, t1, t2, t3, v0, ra
JAL $CalcCursorIntPos %returns on t2
NOP
LIO t0, $DynamicallyGeneratedWarpValues
ORI t1, r0, 0004
MULT t2, t1
MFLO t2
ADD t3, t2, t0 %return address of where to point index to t3
LW t3, 0000 (t3)
LB a0, 0000 (t3)
LB a1, 0001 (t3)
LB a2, 0002 (t3)
POP t0, t1, t2, t3, v0, ra
JR RA
NOP
}

%so its Func:si_CreateScript
%($script, int priority, bool unknown, int groupid)

@Hook 82760
{
PUSH ra
JAL $MainAnimation
NOP
JAL $GlobalInRoom
NOP
POP ra
LW	S7, 0084 (SP)
J 800E92B8
LW	S6, 0080 (SP)
}

@Hook 7A398
{
PUSH t0, ra
LAW t0, $AFlag
BEQ t0, r0, .end
NOP
LUI s0, 0001
ORI s0, s0, 0021
/%LUI s0, 0008
ORI s0, s0, 000D %0008000D mario on zipline animation %/
.end
POP t0, ra
LWC1 F0, 0080 (S1)
SW S0, 00A4 (S1)
J 800E0EF0
NOP
}


@Hook 11554 %[80036154]
{
SAW r0, $AFlag
LUI	AT, 800A
J 8003615C
SH	R0, 0944 (AT)
}


@Hook 11608 %[80036208]
{
LUI v1, 800A
ADDIU	V1, V1, A650
SAW r0, $AFlag
J 80036210
NOP
}


/%
Bits used for map locations (on pause screen map)
800DBD68
1  - Flower Fields
2  - Cloudly Climb
3  - Shiver City
4  - Shiver Snowfield
5  - Starborn Valley
6  - Shiver Mountain
7  - Crystal Palace
8  - Star Way

9  - Star Haven
10 - Bowser's Castle
11 - Princess Peach's Castle
12 - Mario's House
13 - Unused?
14 - Unused?
15 - Unused?
16 - Unused?

17 - 32 Unused?

800DBD64
1 - 8 Unused

9  - Unused?
10 - Unused?
11 - Toad Town
12 - Princess Peach's Castle (Castle Grounds)
13 - Shooting Star Summit
14 - Toad Town Tunnels
15 - Goomba Village
16 - Goomba Road

17 - Pleasant Path
18 - Koopa Village
19 - Koopa Bros. Fortress
20 - Mt. Rugged
21 - Dry Dry Desert
22 - Dry Dry Outpost
23 - Dry Dry Ruins
24 - Forever Forest

25 - Boo's Mansion
26 - Gusty Gulch
27 - Tubba Blubba's Castle
28 - Windy Mill
29 - Shy Guy's Toy Box
30 - Jade Jungle
31 - Yoshi's Village
32 - Mt. Lavalava%/

/%
Map bits used read from by parasol
Pause Screen Map Bits
800DBD68
1  - Flower Fields % 8
7  - Crystal Palace % 9
10 - Bowser's Castle % 10
11 - Princess Peach's Castle % 11


800DBD64
11 - Toad Town %  1
19 - Koopa Bros. Fortress % 2
23 - Dry Dry Ruins % 3
25 - Boo's Mansion % 4
27 - Tubba Blubba's Castle % 5
29 - Shy Guy's Toy Box % 6
32 - Mt. Lavalava % 7 %/

#new:Function $ClearDynamicMenus
{
PUSH s0, s1, v0, v1
LIO s0, $DynamicallyGeneratedWarpValues
LIO s1, $DynamicallyGeneratedTextPointers
ORI v0, r0, 000B
ORI v1, r0, 0001
.loop
SW r0, 0000 (s0)
SW r0, 0000 (s1)
ADDIU s0, s0, 0004
ADDIU s1, s1, 0004
ADDIU v1, v1, 0001 %counter, once it hits 11 (0x0B) exit
BEQ v0, v1, .exit
NOP
BEQ r0, r0, .loop
NOP
.exit
POP s0, s1, v0, v1
JR RA
NOP
}


#new:Data $ANDBitsTable1
{
%800DBD64
00000400 %Toad Town
00040000 %Koopa Bros Fortress
00400000 %Dry Dry Ruins
01000000 %Boo's Mansion
04000000 %Tubba Blubba's Castle
10000000 %Shy Guy's Tox Box
40000000 %Yoshi's Village %Mt. Lavalava %was 80000000
00000000
}

#new:Data $ANDBitsTable2
{
%800DBD68
00000001 %Flower Fields
00000040 %Crystal Palace
00000200 %Bowser's Castle
00000400 %Peach's Castle (in the air)
FFFFFFFF %Pit of 100
FFFFFFFF %Star Temple	
00000000
}


#new:Function $GenerateDynamicWarpMenu
{
PUSH A0, A1, t0, t1, t2, t3, t4, s0, s1, s2, s3, s4, s5, s6, S7
LIO s0, $WarpsValueTable
LIO s1, $TravelTextPointers
LIO s2, $DynamicallyGeneratedWarpValues
LIO s3, $DynamicallyGeneratedTextPointers
LIO s4, 800DBD64 %Map Flags address
LIO s5, $ANDBitsTable1
LIO s6, 40000000 %used to know when the end of table 1 (contains Yoshi's Village item)
LIO S7, FFFFFFFF %indicates no flag check in and table [slot always active]
%ADDITIONAL CONDITIONS:
%Byte 800DBD7B (GameByte 00B) is WarpLock.
% == 0: normal parasol operation.
% == FF: can't warp now.
% == some positive number: only display warping pointer for that one. Skips map flag check. 
LAB		A1, 800DBD7B   %load WarpLock
BGEZ 	A1, .loop  %special lock is handled in loop
ADDIU	A0, R0, 1	   %setting A0 to 1 to count which location we're on
%general warp lock
LIA 	T3, $NoWarpText
BEQ		R0, R0, .end
SW	 	T3, 0 (S3)
%only one value will be displayed.
.loop % i think
LW t0, 0000 (s4) %load first 4 bytes from map flags address
LW t4, 0000 (s5) %load value from AND table
BEQ T4, S7, .alwaysactive %if value loaded is FFFFFFFF, we don't need to check if we've visited this location.
NOP
BEQ t4, s6, .changeregisterS5 %if value loaded is 80000000 (end of ANDI table 1)
NOP
BEQ t4, r0, .end %if end of AND table, exit
NOP
.afterchangingS5
AND t1, t0, t4 %if bit is 1 in both t0, t4
BEQ t1, r0, .notactive
NOP
.alwaysActive
%now we check for special lock
LW t2, 0000 (s0) %load pointer from $WarpsValueTable
BLEZ	A1, .normalWarpType
LW t3, 0000 (s1) %load pointer from $TravelTextPointers
%if we're here: special lock instead
BEQ		A0, A1, .normalWarpType %if this is the one warp-allowed destination, redirect to normal 
NOP
ADDIU	S1, S1, 38 		%adds 56`/ 4 = 14 places forward to get 'disabled' string value
LW 		t3, 0000 (s1) 	%load pointer from $TravelTextPointers
ADDIU	S1, S1, FFC8  	%reverses addition
CLEAR	T2				%also clear warp dest value
.normalWarpType
SW t2, 0000 (s2) %save pointer to $DynamicallyGeneratedWarpValues
SW t3, 0000 (s3) %save pointer to $DynamicallyGeneratedTextPointers
ADDIU s2, s2, 0004
%BEQ r0, r0, .loop
ADDIU s3, s3, 0004
.notactive
ADDIU A0, A0, 1    %don't forget to keep track of which item in the table we're pointing to!
ADDIU s0, s0, 0004 %next pointer in WarpValuesTable
ADDIU s1, s1, 0004 %next pointer in TravelTextPointers
BEQ r0, r0, .loop
ADDIU s5, s5, 0004 %next pointer in ANDBitsTable1
.changeregisterS5
LIO s5, $ANDBitsTable2
ADDIU s5, s5, FFFC
BEQ r0, r0, .afterchangingS5
ADDIU s4, s4, 0004
.end
POP A0, A1, t0, t1, t2, t3, t4, s0, s1, s2, s3, s4, s5, s6, S7
JR RA
NOP
}

#export $AFlag
#export $WarpFunction
#export $CursorAnimation
#export $CalcCursorIntPos
#export $DynamicallyGeneratedWarpValues

#export $GiveMarioInvisibiltyToEnemies
#export $RemoveMarioInvisibilityToEnemies


